# Author: Ali Uneri
# Date: 2012-06-09

set(EP_OPTION_NAME "USE_${EP_NAME}")
set(EP_REQUIRED_PROJECTS Git MATLAB Python)
set(EP_URL "git://github.com/auneri/mlabwrap.git")
set(EP_PATCH "${CMAKE_CURRENT_LIST_DIR}/Patches/${EP_NAME}.patch")

cma_envvar(@LIBRARYPATH@ PREPEND "@INSTALL_DIR@")
cma_envvar(PYTHONPATH PREPEND "@INSTALL_DIR@")
if(WIN32)
  cma_envvar(@LIBRARYPATH@ APPEND "${${PROJECT_NAME}_MATLAB_DIR}/bin/win64")
elseif(APPLE)
  cma_envvar(@LIBRARYPATH@ APPEND "${${PROJECT_NAME}_MATLAB_DIR}/bin/maci64")
elseif(UNIX)
  cma_envvar(@LIBRARYPATH@ APPEND "${${PROJECT_NAME}_MATLAB_DIR}/bin/glnxa64")
else()
  message(FATAL_ERROR "Platform is not supported.")
endif()

cma_end_definition()
# -----------------------------------------------------------------------------

if(WIN32)
  file(TO_NATIVE_PATH ${PROJECT_BINARY_DIR} PROJECT_BINARY_NATIVE_DIR)
  set(EP_BUILD_COMMAND
    set PYTHONPATH=${PROJECT_BINARY_NATIVE_DIR}/${EP_NAME}-install &&
    ${${PROJECT_NAME}_PYTHON_EXECUTABLE} setup.py
      build --build-lib=${PROJECT_BINARY_NATIVE_DIR}/${EP_NAME}-build
            --build-temp=${PROJECT_BINARY_DIR}/${EP_NAME}-build
      build_ext --compiler=msvc
      install --install-lib=${PROJECT_BINARY_NATIVE_DIR}/${EP_NAME}-install
      --matlab=${${PROJECT_NAME}_MATLAB_EXECUTABLE})
else()
  set(EP_BUILD_COMMAND
    export PYTHONPATH=${PROJECT_BINARY_DIR}/${EP_NAME}-install &&
    ${${PROJECT_NAME}_PYTHON_EXECUTABLE} ./setup.py
      build --build-lib=${PROJECT_BINARY_DIR}/${EP_NAME}-build
            --build-temp=${PROJECT_BINARY_DIR}/${EP_NAME}-build
      install --install-lib=${PROJECT_BINARY_DIR}/${EP_NAME}-install
      --matlab=${${PROJECT_NAME}_MATLAB_EXECUTABLE})
endif()

ExternalProject_Add(${EP_NAME}
  DEPENDS ${EP_REQUIRED_PROJECTS}
  # download
  GIT_REPOSITORY ${EP_URL}
  GIT_TAG c9105fe06e9bcdbdf9600087f1c683866cead244
  # patch
  # update
  UPDATE_COMMAND ""
  # configure
  SOURCE_DIR ${PROJECT_BINARY_DIR}/${EP_NAME}
  CONFIGURE_COMMAND ""
  # build
  BUILD_COMMAND ${EP_BUILD_COMMAND}
  BUILD_IN_SOURCE 1
  # install
  INSTALL_DIR ${PROJECT_BINARY_DIR}/${EP_NAME}-install
  INSTALL_COMMAND ""
  # test
  )
